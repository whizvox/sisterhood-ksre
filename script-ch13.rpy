label sisterhood_ch13:
label .sh_ch13:

$ set_window_tint(TINT_HANAKO)

call sisterhood_timeskip

scene bg school_newspaper
with Dissolve(2.0)

play music music_comedy fadein 0.5

show naomi bend_laugh at offscreenright
with None
show naomi at tworight
with charamovefastest

na "Hey, Hanako."
"I deliver a quick nod as the president and editor-in-chief of the newspaper club comes barging into the computer room."
ha "N-Naomi."
"Ever since I joined the club as a permanent member, Naomi insisted I'd be on first-name basis with the other members, though I'm still having a hard time getting used to that."
"It's easier with Naomi, though, since my tasks usually require regular input from her, and the fact that she's my neighbor in class also doesn't hurt. We're familiar enough with one another nowadays for me to occasionally talk to her in class and do the occasional assignment with her and Natsume."
"One thing that's usually an advantage is that there are hardly any silences, uncomfortable or otherwise, around her."
"I suppose we could be called semi-friends now, though our contact is a lot more casual than my bond with Hisao and Lilly, and I still have trouble dealing with her occasionally hyperactive moods and bouts of distraction from time to time."

show naomi bend_smile
with chchange

na "Hey, how's page four coming along?"
ha "Almost ready."
na "Cool. Get me a print-out when it's finished. I can't wait to check it out."
"I don't think she's here just to ask about one of the pages."
"Either she's bored, which usually causes her to start chattering up whoever happens to be nearby, or there's more she wants to discuss."

show naomi basic_neutral
with chchange

na "Oh hey, about your article. I read it through just before the weekend."
ha "Y-yes?"

show naomi basic_smile
with chchange

na "I like it. I took a lot of rewording, didn't it? I mean, that text doesn't look like stuff Mutou would say."
ha "He gets a b-bit technical at times. I tried to make it easier to understand. I had to trim a lot though."
na "I could get you some more space if you can use it."
ha "Oh?"

show naomi basic_neutral_close at center
with charamovechangefaster

"She sits on my desk and tosses me a wink."
na "I've been thinking. Maybe instead of making it a short article, we could turn it into a column."
na "A permanent one. Or semi-permanent until we got a few test runs. We could put it on the columns page, or let it sit on page five. Each issue would cover a different club here at school."
na "The idea would be to give a short overview of each club written in a way to encourage casual readers to check it out. Like you've been doing."
"Looks like Naomi's been thinking about this a lot more than I have."
"All I wanted was to do a little piece on Hisao's club and hopefully get him a new member or two as a little “I love you”-gesture towards him."
"Naomi's ambitions, however, seem to reach further."

show naomi basic_grin_close
with chchange

na "In fact, seeing that we'd be benefitting the school by actively encouraging club membership, that'd give me a lot of extra clout to get our budget raised when the time comes to negotiate with the student council about club budgets again."
"Quite a bit further."
na "In fact, I bet if we offer to cover the student council itself in there, Queen Hakamichi might throw in a personal bonus. I know she'd certainly welcome extra membership."
"Naomi scratches her cheek, chuckles and sends me a clownish grin."

show naomi basic_laugh_close
with chchange

na "The question remains of course whether the extra membership would welcome {i}her{/i}. If you can write something up that gets Hakamichi additional personnel, I'll personally nominate you for a Pulitzer."
"Me… writing?"
na "Of course, I'm not really in a position to nominate people for that."
"Slow down a bit."
na "And even if I was, isn't that prize US-exclusive?"

show naomi basic_focus_close
with chchange

na "Err… Where was I?"
ha "Ummm… M-me writing something up?"

show naomi basic_neutral_close
with chchange

na "Right. Seeing that this started with your initiative, it's fair to ask you first if you're interested in being the one in charge of writing it up every issue."
na "I mean, you're a pretty good text writer, I've seen that much in class already."

show naomi basic_smile_close
with chchange

na "It's a shame if all you do here is data entry and fiddling with the layout. It's also good if the one writing it is always the same person, so the writing style remains consistent."
"Me?"
na "You liked writing that piece. Why not do more with it?"
ha "…maybe…"

show naomi basic_neutral_close
with chchange

na "Not overly enthusiastic, I see."
ha "…It's not that…"

show naomi at sittingpos
with charamove

"Naomi gets off the desk, pulls a chair over, turns it around and sits down on it, her elbows resting on the back rest."
na "You've been kinda gloomy lately."
na "Well, it's not like you haven't had gloomy periods before, but…"
na "This is about Satou, isn't it? Isn't she going away tomorrow?"
ha "Y-yes. Tomorrow evening…"
"Naomi lets out a sad sigh."
na "Sometimes you just wish you could freeze time, don't you? Just… keep the good times in your life from ending."
ha "…yes…"
na "In the upcoming half year, I bet a lot of third year students will be thinking that as graduation approaches. With you, it just comes around a little earlier."
ha "…you too?"

show naomi basic_smile_close
with chchange

na "Well, I don't think after school, I'll have time to play newspaper anymore, that's for sure. Pity too."
"That's news to me. I always assumed Naomi was going to go into journalism. If anybody was into the subject, it'd be her."
ha "Y-you're not going to b-become a journalist?"
na "Dunno. Maybe. It might not work. The loose wiring upstairs might cause problems."
"Her epilepsy? I admit her case is a pretty severe one, but…"

show naomi basic_laugh_close
with chchange

na "The episodes itself are one thing. I've heard it's pretty freaky for others."
na "Me, I just go out like a light and wake up feeling like crap for a while without remembering what happened. But it comes with restrictions."
ha "Restrictions?"

show naomi basic_smile_close
with chchange

na "You know, don't sit in front of a screen for too long, don't get your driver's license, live your life as regularly as possible, no all-nighters… That sort of thing."

show naomi basic_neutral_close
with chchange

na "Going professional… may not work out very well."
"Every student has a history. I've just learned part of one more."
"I wish the situation was different for me right now. With enough on my mind as it is, I really don't know what to say to her."
"Listening is something I can do well. Comforting is a whole different matter."
ha "…"

show naomi bend_smile_close
with chchange

na "You know, I hope at least you get to enjoy your little party tonight?"
ha "Y-you know… about… that?"
na "Yeah, your boyfriend said you two were going shopping for that after you're done here."
na "He's waiting for you at the school gate. I couldn't let him in to tell you in person, 'cause I was afraid he'd spot your article lying on my desk."
"Why didn't you tell me until now!?"

show naomi basic_focus_close
with chchange
with Pause(0.5)
show naomi basic_smile_close
with Dissolve(0.2)

"She looks pensive for a moment and suddenly snaps her fingers, causing me to flinch."
na "Oh right, that's why I came in here. To tell you that."

show naomi bend_laugh_close
with chchange

na "Ha ha… I guess I got a little sidetracked, huh?"
"OH, FOR CRYING OUT LOUD!"

stop music fadeout 0.5

scene bg school_gate_rn
show hisao basic_grin_uni_rn
with locationskip

play music music_soothing fadein 0.5

hi "Couldn't get away from Naomi, could you?"
"Hisao seems amused by my tardiness and the fact I'm clearly winded from my mad dash to the gates."

show hisao basic_smile_uni_close_rn
with characlose

"After handing me one of the two shopping bags he's carrying, he bends forward and gives me a quick peck on the cheek while I'm busy catching my breath."
ha "She… forgot… for a while."
"Hisao nods. He doesn't interact with Naomi much, but has heard several stories from me over the last few weeks."

show hisao basic_neutral_uni_close_rn
with chchange

hi "We'd better get going quickly if we want to finish everything in time."
ha "Yes."

hide hisao
with charaexit

"We leave the school grounds and walk down the road to town."
"Despite the fact that it's summer, the sky is a bit dark and about as gloomy as my mood."
"I'd be surprised if we make it through the evening without a major downpour."

scene bg school_road_rn
with locationchange

hi "Hanako?"
ha "S-sorry, what were you saying?"

show hisao basic_neutral_uni_close_rn at twoleft
with charaenter

hi "I was asking if we should get an additional bag of sweets. She seemed to like that brand last time, and we went through it pretty quickly."
ha "If you think so."
hi "Maybe we should take some time off after this week."
ha "Hmmm?"
hi "You've been gloomy all week, and frankly I'm been getting a bit worried."
"He's worried about me. Almost seems like old times…"
"No, I shouldn't think like that."

show hisao basic_smile_uni_close_rn
with chchange

hi "You know I'm here for you if you need me, right? If you want to talk about something…"
"Part of me wants to share the anxiety I've been feeling with Hisao. If I can't even trust him with it, who can I trust? But I'm also afraid he'll just tell me I'm imagining things."
ha "Erm… Hisao. Do you think the time I spent with Lilly lately was… a good idea?"

show hisao basic_speak_uni_close_rn
with chchange

hi "I think so. Why do you ask?"
hi "Do you regret all those activities? I thought you had a good time yourself."
ha "I did enjoy them. It's just…"
ha "…they… didn't really… help things."
hi "Well, if you look at pure end results, they didn't do anything to change Lilly's mind, but we'll never know what the odds of that happening were to begin with."

show hisao basic_smile_uni_close_rn
with chchange

hi "Regardless of that, at least you and Lilly made some wonderful memories together, didn't you?"
hi "I mean, isn't that kinda the key to enjoying life? Making fond memories because you know nothing lasts truly forever?"
"It's not really that."
ha "I suppose so…"
hi "Hanako?"

show hisao basic_neutral_uni_close_rn at center
with charamovechangefaster

"He stops me and faces me. He looks at me for a moment and then gives me a gentle hug."
hi "I'm not gonna tell you to cheer up, because you have a good reason to feel down."
hi "I just hope you'll feel better soon."
ha "T-thanks."

show black
with shuteye

"I close my eyes for a few moments and let the feeling of his embrace sink in, providing some comfort."
ha "Hisao?"
hi "Yeah?"

show hisao
with None
hide black
with openeye

ha "Parties are for celebrating something, right?"
ha "B-but, is there really something to celebrate?"

show hisao cross_grin_uni_close_rn
with chchange

hi "Her class will be throwing her a surprise party tomorrow. Kenji was complaining about it earlier. We can't really fall behind, can we?"
ha "We're supposed to be cheerful and smile. Even if we don't feel that way."
ha "It feels… fake. Like…"

show hisao cross_neutral_uni_close_rn
with chchange

hi "Like…?"
ha "Like… many of my previous b-birthday celebrations. I'm… not very good at pretending."

show hisao cross_speak_uni_close_rn
with chchange

hi "Maybe we shouldn't call it a party or a celebration then. It's just… one last opportunity to spend time together."
hi "You'd want to make the most out of an occasion such as that, wouldn't you?"
"That makes me think for a bit."
"If I see it as just hanging out together… It's a silly game of words, but it worked with my birthday party."

show hisao basic_smile_uni_close_rn
with chchange

hi "So, Hanako, do you think one bag of sweets is enough for an entire evening?"
ha "Ummm… We can probably get a second one just to be sure."
hi "Got it."

show hisao basic_grin_uni_close_rn
with chchange

"He ruffles my hair for a bit and is about to let go when he suddenly freezes."

stop music fadeout 0.5

show hisao basic_neutral_uni_close_rn
with chchangefast

hi "…Did you just feel something?"
ha "Hmmm?"
"I look up towards the sky and sure enough a droplet lands on my nose causing me to back out of our embrace and rub it off."

show hisao basic_frown_uni_close_rn
with chchange

hi "Oh great, just what we needed. And it's still nearly ten minutes to the store."
ha "Should we go back?"

show hisao basic_speak_uni_close_rn
with chchange

hi "I'm not sure, it's also about ten minutes back to Yamaku and it'll be ten minutes uphill. And we still need to get goods from the store."
ha "But we might get trapped in town."
hi "If it continues to rain, we might be able to get an umbrella. Maybe Yuuko can lend us one if she's on duty in the Shanghai."

hide hisao
with charaexit

"We continue our way down the road, our pace increased to a brisk walk."

play ambient sfx_rain fadein 5.0
show rain normal
with Dissolve(5.0)

"My hopes that we'll reach the store relatively dry shrink with every passing minute as the drizzle changes to rain and the rain then turns into a downpour."

show hisao basic_frown_uni_close_rn behind rain
with charaenter

hi "Damnit, we're gonna get soaked and spend the next few days with pneumonia if this keeps up."

show hisao basic_speak_uni_close_rn
with chchange

hi "Hanako, do you think you can manage to sprint the last stretch?"
ha "S-sprint? That's irresponsible, Hisao. Your heart…"

show hisao basic_grin_uni_close_rn
with chchange

hi "Look, I run nearly every morning, and I haven't had a single flutter. I can handle it. Now can you manage to run for a bit?"
ha "I-I'll try."

show hisao at offscreenright:
    alpha 0.0
with charamovechangefastest

"Hisao nods and launches into a run that I try my hardest to keep up with."
"He's in much better shape than I am, that's for sure."

show bg school_road_run_rn behind rain
with locationchange

"Despite the fact Hisao once joked that I could probably outrun Emi Ibarazaki under the right circumstances, I'm not particularly athletic by anyone's standards, except maybe Lilly's."
"The head nurse was so kind as to spare me the horrors of having to walk in gym shorts and a shirt and have my stiff movement, caused by my scar tissue, laughed at and mocked by classmates, as was the case in middle school."
"As it is, the question isn't whether I'll fall behind but when."

show hisao basic_speak_uni_close_rn behind rain at twoleft
with charaenter

hi "You okay?"
"I barely have enough breath to manage a reply."
ha "Y-yes. Don't mind me."

scene bg suburb_roadcenter_run_rn
show rain normal
with locationchange

"We're reaching the edge of town. Just another minute or two and we can take shelter."
"I'm panting heavily and I'm betting that my clothes aren't just soaked with rain right now."
"We're running as fast as we can, heads down, hand carrying the shopping bag raised in an attempt to shield ourselves from the ruthless elements."
"I look at Hisao who's running slightly ahead of me. He's looking back at me over his shoulder, probably about to ask if I'm alright and if I can manage for just a little longer."
"That's when I suddenly see a silhouette appear in front of him, approaching us at breakneck speed."
"My eyes grow large and I try to shout a warning."
ha "{size=+10}WATCH OUT!{/size}"

play sound sfx_bicyclecrash

$ renpy.music.set_volume(0.0, channel="ambient")
with Pause(0.6)
scene black
with None
with Pause(3.0)

$ renpy.music.set_volume(1.0, delay=5.0, channel="ambient")
with Pause(5.0)
scene bg suburb_roadcenter_rn
show rain normal
with openeye

"When I open my eyes again I see Hisao lying on the ground next to a fallen bicycle and another person, a man in his fifties from the look of it."
"Both are looking pained, the other man cradling his leg and Hisao clutching his chest."
"The cyclist is probably a resident here, surprised by the weather and doing the same thing we were doing; trying to find shelter as quickly as he could."
"Hisao's clutching his chest."
"Hisao doesn't look like he's getting up. Did he get injured?"
"He's clutching his chest!"
"I hurry over to him and kneel at his side."
"{size=+10}{i}He's clutching his chest!{/i}{/size}"

show ev rainyroad
with mediumflash

$ renpy.music.set_volume(0.7, channel="ambient")
play music music_tragic

ha "{i}No!{/i}"
"A single, desperate exclamation escapes my mouth as I realize what's happening to Hisao."
"With that realization comes a sharp pain in my own chest as if one of my ribs just snapped and punctured my heart."
"{i}This can't be happening!{/i}"
"Hisao's breathing is strained. I want to help him, but I'm completely lost at what to do."
"{i}He's going to die!{/i}"
"He raises his head for a moment and looks up at me."
"I want to reach out to him, but my body won't obey… Its only reaction a frantic trembling."
"{i}Why can't I move? Somebody help him! Please!{/i}"
"His eyes close again. The pain in my chest grows so intense that tears are forced out of my eyes, joining the raindrops on my cheeks."
"Am I having a heart attack as well?"
om "Young lady, is your friend alright? I'm sorry I didn't see you two in time. I was in a hurry. That blasted rain."

show ev:
    zoom 1.2 align (0.39, 0.22)
with Dissolve(1.0)

"{i}He's dying.{/i}"
"{i}I'm losing him.{/i}"
"{i}Like I'm losing Lilly.{/i}"
"I'm overcome by an intense sensation of fear, like ten years ago when Mother scrambled in front of me, and we screamed as the flames got to us."

show black:
    alpha 0.2
with Dissolve(1.0)

"Like I lost Father. And Mother."

show black:
    alpha 0.4
with Dissolve(1.0)

"I'm breathing in and out like crazy, yet it feels like all oxygen has suddenly vanished from the air."
om "Young lady, your friend looks like he's hurt."
"Like I lost everyone."

show black:
    alpha 0.6
with Dissolve(1.0)

"I feel like I'm suffocating. With each fruitless gasp for breath I'm getting more and more terrified."

show ev:
    zoom 1.5
with Dissolve(1.0)

"{i}Stop!{/i}"
"{i}Make this stop!{/i}"
"{i}Help me!{/i}"
om "Young lady, I think we should call an ambulance."

$ renpy.music.set_volume(0.5, 1.0, "ambient")
show black:
    alpha 0.8
with Dissolve(1.0)

"I still can't move."
"I can't breathe, but I can't faint either as if some twisted deity has decided it's more amusing to keep me fully aware as the love of my life is dying in front of me and is keeping me conscious for the sheer cruel fun of it."
om "Young lady?"

show ev:
    zoom 1.8
with Dissolve(1.0)

"{i}I can't breathe.{/i}"
om "Young lady, can you hear me?"
"{i}Why can't I breathe?{/i}"
om "{i}Young lady!{/i}"

show black:
    alpha 1.0
with None
stop music
stop ambient

with Pause(1.0)

"{size=+10}{i}{cps=5}Just let me breathe!{/cps}{/i}{/size}"

scene black
with Dissolve(3.0)

return