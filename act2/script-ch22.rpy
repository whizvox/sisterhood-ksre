label sh_ch22:
    label .s1:

        $ set_window_tint(TINT_HISAO)

        call sisterhood_timeskip

        scene bg satou_guestroom
        with Dissolve(2.0)

        play music music_serene fadein 4.0

        nvl clear
        nvl show dissolve

        n "As I open my eyes and gaze at a completely unfamiliar ceiling, I'm unaware where I am for a few moments."
        n "{vspace=60}The room I've woken up in reminds me more of a hotel room, with its large bed, a small table with two chairs near one corner and a doorway leading to a niche with a sink and a small shower stall inside, than an ordinary bedroom."
        n "The curtains in front of the window facing the bed are still closed, but judging from the amount of light they're letting through, it's definitely not morning anymore. The dim light and the pleasant temperature give the room a cozy feeling."
        n "For as long as I've known her, Lilly has always carried herself with a certain air of nobility, and when she shelled out a whopping 20,000 yen for Hanako's birthday present without a moment of hesitation, it merely confirmed my suspicions that Lilly came from a family that could be categorized as upper class."
        n "The fact that her family owned a summer home and didn't seem to mind paying for two intercontinental flight tickets changed my expectations from ‘not strapped for cash’ to ‘rich’. Still, when Akira dropped us off at the Satou family's residence, I was taken aback by the size of the building. I think the word ‘mansion’ would not be an exaggeration."

        nvl hide dissolve

        "The long journey here had left the three of us so extremely exhausted that after Akira bid us farewell, we pretty much went to bed immediately without waiting for Lilly's parents to come home."
        "I look at the chairs in the corner again. My own clothes are folded over one of them. The other holds Hanako's clothes."
        "I softly turn my head to the right, and I can see my girlfriend lying next to me. She's currently lying on her stomach and seems occupied scribbling words onto a small blocknote in front of her."
        "Since she's not wearing her hair clip, and a lock of hair is obscuring the right side of her face, it appears she hasn't noticed me waking up yet."
        "With the covers on her side of the bed pulled back and with nothing on except her panties and a T-shirt, Hanako is a pleasant sight to wake up to."
        "Taking care not to alert her, I sneak a hand over to the left side of her lower back and playfully tickle her there."

        show hanagown worry_blush_close
        with Dissolve(0.2)

        "Hanako responds with a cry of surprise, turns around and quickly pulls the covers up to her chest."

        show hanagown drunkpout_close
        with chchange

        "She then looks at me and does her best to pout at me which looks more cute than intimidating."

        show hanagown smile_close
        with chchange

        "When I smile in amusement, she can't help smiling back."
        hi "Hey there."
        ha "H-Hey. You're finally awake."
        "The Hanako lying next to me right now seems completely different from the way she was yesterday."
        "While we were in the process of navigating the airports, Hanako seemed continuously in what I came to call ‘survival mode’."
        "Hat pressed down, eyes trained on the floor, gaze jumping back and forth as if scanning the ground for landmines, and completely focussed on fighting off her anxieties that were trying to get a hold of her flight reflex."
        "Right now though, she seems at ease, relaxed and there's a small twinkle in her eye as if she's eager to drag me out of bed and start experiencing our vacation for real."
        hi "Finally? What time is it?"

        show hanagown worry_close
        with chchange

        ha "It's nearly five o' clock in the afternoon."
        hi "Five o' clock in the afternoon already? Ugh, my sleeping patterns must have been more messed up than I thought."
        "I grin at Hanako embarrasedly."
        hi "I hope you didn't think I died in my sleep or something."

        show hanagown smile_close
        with chchange

        ha "I... did... check your vital signs a f-few times... just to be s-sure."
        hi "Gee, thanks for the vote of confidence."

        show hanagown normal_close
        with chchange

        ha "Sleeping for nearly 24 hours straight isn't really n-normal."
        hi "I didn't exactly sleep 24 hours. It was more like 8 hours, followed by 10 more. I've been awake for most of the night."
        "Hanako's expression shows a hint of worry upon hearing this."

        show hanagown worry_close
        with chchange

        ha "Did j-jetlag hit you that hard?"
        hi "It's probably more than just jetlag in my case."
        hi "I've been busy switching back to my regular medication over the last few days and due to the trip and the time differences, I wasn't able to take my meds according to the usual schedule yesterday. My chemical balance was probably as stable as a chair with a missing leg."
        ha "How about now?"
        hi "I took my meds before going back to sleep. I'll take my next batch after dinner. I'll be okay."

        show hanagown normal_close
        with chchange

        ha "Did you just sit here all the time while you were awake?"
        hi "At first I did, but then I decided to take a restroom break, and on my way back I ran into Lilly who heard me flush the toilet."
        hi "Turns out she couldn't sleep either, so she ended up giving me a tour of the house. Seeing that we went to bed almost immediately after getting here, I appreciated the chance to sightsee."
        hi "Afterwards we spent time hanging out in the living room until we got sleepy again. Which was probably around half past 6 or so."

        show hanagown distant_close
        with chchange

        "Hanako looks dejected at this."
        ha "I wish you'd have woken me up. I wanted to hang out too."
        hi "You were looking so cute in your sleep, I couldn't bring myself to wake you. Besides, we both agreed that you're the one with the healthiest sleeping pattern right now."

        show hanagown normal_close
        with chchange

        "Hanako considers this for a moment, then nods."
        ha "So ummm... What's the house like? I would have liked a tour myself."
        hi "It's pretty large. Large enough to cause Lilly to have the occasional bit of trouble navigating it."
        hi "Seems like there are three bedrooms aside from the master bedroom including this one. Lilly slept here during her last trip, but since no other guest room has a king-sized bed, they decided to let us have this one."
        hi "The living room is really nice. Big fireplace, soft couches. The kitchen is pretty large too. Two fridges, a kitchen island with several stoves and according to Lilly, they have a cellar with a wine rack too."

        show hanagown worry_close
        with chchange

        ha "It seems... very... large for just two people."
        hi "According to Lilly, they employ two servants as well as a housekeeper who does the cooking most of the time."
        ha "Wow."
        hi "Did you know Lilly's family was this wealthy?"

        show hanagown normal_close
        with chchange

        "Hanako thinks for a bit and then nods."
        ha "Lilly's not very comfortable with it. I got a general idea from some of Akira's stories."
        hi "You're gonna be surprised when you see the study and the bathroom."
        ha "How so?"
        hi "Most of the house is built in western style, but the study and bathroom are in traditional Japanese style. It's a very weird contrast, almost as if the house has a split personality. Lilly said her father still seems to have a strong emotional connection to his home culture."

        show hanagown worry_close
        with chchange

        ha "Have you m-met Lilly's parents already?"
        hi "No, I haven't. Lilly said they briefly visited her room to welcome her when they came home last night, but when I woke up it was around midnight, and they already went to bed. I don't think they felt like waking us up just to say hello."
        ha "I am a little bit curious about them."
        hi "Just them?"

        show hanagown smile_close
        with chchange

        "Getting my point, Hanako smiles excitedly."
        ha "A lot of other t-things too."
        hi "I guess we'd better get out of bed then, huh?"
        "I reach out and playfully stroke the top of her head. As I do so, I notice her hair is slightly moist."
        hi "You already took a shower?"
        ha "You should t-take one too. It's nearly time for dinner already."
        
        hide hanagown
        with charaexit

        "She gets out of bed, walks up to the dresser and takes a bath towel out of it."

        show hanagown smile_close
        with charaenter

        "As I get my clothes off the nearby chair, take off my shirt and make my way to the shower stall, she hangs the towel over my shoulders."
        hi "By the way, Hanako, how long have you been up?"
        "She doesn't respond but instead tugs both ends of the towel gently, drawing my face close to hers, and gives me a light peck on the lips."
        "I suppose she's not gonna tell me, but judging from the position of the bookmark in the book on her nightstand, she's been awake a lot longer than I have been."

        hide hanagown
        with charaexit

        "I take a quick shower, dry myself off, put my underwear back on and am about to put on the rest of my clothes when a thought suddenly hits me."
        "Most of my medication is still in my suitcase. If it stays in there, I might end up forgetting about it again just like what was nearly the case when we went to Hokkaido. It'd be really inconvenient if I ended up in the hospital here."
        "I quickly head towards my suitcase in search of my pill collection."
        hi "Hey Hanako, do you mind if I put my medication on the little shelf above the sink so I won't…"

        stop music fadeout 1.0

        # TODO Use Hanako and Lilly's casual 2 sprites

        show hanako defarms_shock_cas at tworight
        show lilly cane_smile_cas:
            xalign 0.1
        with charaenter

        li "Ah. Good afternoon, Hisao."
        "My sentence is cut short when I emerge from the doorway, and I find not just Hanako but also Lilly standing there."
        "Well, Lilly's actually still standing in the doorway. How the heck did I miss the sound of her knocking?"
        "As she observes me standing there in nothing but my boxers, Hanako's eyes grow wide and she puts her hand in front of her mouth in order to suppress a gasp."
        hi "Lilly!"
        "Hanako's eyes dart back and forth between Lilly and me. I can tell she's trying to figure out what to say or do, but she doesn't seem quite able to think of anything."
        "Obviously, Lilly can't see me, though I bet she'll deduce that something's off if both of us just keep standing there without saying a word."
        "I swallow quietly and do my best to sound as casual as possible."

        play music music_comedy fadein 4.0

        show hanako def_worry_cas
        with chchange

        hi "Sorry, I didn't hear you knock. Did I interrupt you two?"

        show lilly cane_smileclosed_cas
        with chchange

        li "Not at all. I merely came to tell you and Hanako that dinner will be served in half an hour. It's been a rather warm day today, and it will apparently remain pleasant for most of the evening, so you can probably afford to change into some light clothing if you haven't done so already."
        hi "Thanks. I'll keep that in mind. Will we be eating in the dining room?"
        li "I've asked if we can have our meal on the patio behind the house. Apparently the view is quite lovely, and some fresh air will do us good after spending an entire day indoors."
        hi "Works for me. Hanako?"
        ha "S-Sure. I'm g-getting a bit hungry t-too."

        show lilly cane_cheerful_cas
        with chchange

        li "Very well. I will see you in thirty minutes then."
        hi "…Right."

        hide lilly
        with charaexit

        stop music fadeout 2.0

        "Giggling softly, Lilly turns around, feels her way over to the door and leaves the room."
        "I quickly retrieve my pill bottles and start getting dressed. Hanako lets out a loud sigh."

        show hanako emb_blushtimid_cas
        with chchange

        ha "That w-was… awkward."
        hi "It kinda was. We got away with it though."
        ha "Thank goodness."

        if _in_replay:
            return

    label .s2:

        $ set_window_tint(TINT_HISAO)

        scene bg satou_patio
        show hanako basic_worry_cas at tworight
        with locationskip

        play music music_soothing fadein 4.0

        "As Hanako and I make our way to the sliding doors leading outside, we can see someone in the distance standing by the table on the patio."
        "Hanako leans in and whispers to me."
        ha "That's not Lilly's m-mother, is it?"
        hi "Dunno. I don't think so. She has brown hair…Lilly's mom is probably blonde, isn't she?"
        ha "The maid then?"
        hi "Most likely. I wonder if she speaks Japanese."
        ha "I don't think she does."

        show hanako cover_worry_cas
        with chchange

        "We walk outside and the woman near the table, who upon closer inspection indeed seems to be wearing a servant's uniform, turns around and makes a deep and polite bow before addressing us."
        "Maid" "Welcome."
        hi "Hey, you speak Japanese! That's a relief. I'm not exactly a fluent English speaker."
        "My hopes are quickly dashed when she sheepishly shakes her head and starts talking to us in English."
        hi "Whoa there! Hey Hanako, can you tell what she's saying?"

        show hanako emb_timid_cas
        with chchange

        ha "Ummm… She's s-speaking rather quickly, but I think she's s-saying she only knows a few s-standard phrases in Japanese."
        hi "That's a shame. Err… The English say ‘How are you?’ and ‘Fine, thank you.’ when they greet each other, don't they?"
        ha "I d-don't think it's p-pronounced that way."
        "While the two of us are still figuring out how to communicate with the woman in front of us, she suddenly bows again and delivers another greeting, one in English this time."

        show hanako cover_distant_cas
        show lilly cane_displeased_cas at twoleft
        with charaenter

        "We look behind us and see Lilly standing there. As I listen to her replying to the woman, I notice she appears a little disappointed."
        hi "Lilly! Good timing. What are you telling her?"

        show lilly cane_weaksmile_cas
        with chchange

        li "I was telling her it was a shame she gave away my presence to you. I had been hoping to hear how far you'd get in your communication with her."
        "For a moment, I wonder if she's teasing us, but Lilly seems entirely sincere."
        hi "Well, now that you're here anyway, would you mind doing the introductions?"

        show lilly cane_smileclosed_cas
        with chchange

        "Lilly nods and starts talking to the servant woman. It's not easy to follow her, but we can clearly make out our own names among her words. The woman smiles and repeats our names."
        "Maid" "Hanako and Hisao."

        show hanako cover_bashful_cas
        with chchange

        "We both smile a bit at hearing our names spoken in such a foreign accent."

        show lilly cane_smile_cas
        with chchange

        li "Hanako, Hisao… This lady is called Allison. She's my parents' housekeeper and coordinates the rest of the staff. She's also in charge of making whatever purchases the household needs and is often tasked with the meals as well."
        li "It is her cooking skill we'll be enjoying during our stay here. I have learned during my last trip here that she's a very good cook."
        hi "Hello Allison."
        ha "H-How d-do you do, Allison?"
        "Allison smiles at our attempts to get her name right and from her subtle grin, I can tell we're mangling the pronunciation a bit."

        show lilly cane_smileclosed_cas
        with chchange

        li "Her name can be tricky for Japanese to pronounce correctly, but you still did rather well."
        "With the formalities out of the way, the housekeeper motions us to sit at the table which has already been laid and, as Lilly prepares to take a seat, draws up a chair to assist her."

        show lilly basic_smileclosed_cas
        with chchange
        
        "Before walking back into the house, Allison turns to us and gives us a short explanation which makes little sense to me but results in an approving nod from Lilly."
        hi "What is she saying?"
        li "We'll be having chicken soup to start with, followed by freshly made pork pie as the main course and pumpkin crumble for dessert. She hopes we'll enjoy it."
        hi "It sounds good."

        show hanako basic_bashful_cas
        with chchange

        # TODO Add patio BG which includes outdoor dining table

        "Hanako and I sit down, Hanako picking the seat that makes her right side face away from the house."

        show lilly basic_cheerful_cas
        with chchange

        li "The Scottish kitchen may take some getting used to if you've lived in Japan all your life. But if it's not to your tastes, don't hesitate to let Allison know."
        hi "I hope it won't come to that. With the level my English skill is at, what's meant to be ‘Could I have something else for my next meal?’ could very well come out as ‘Your nose reminds me of a pork sausage’."

        show hanako basic_smile_cas
        show lilly basic_giggle_cas
        with chchange

        "Lilly giggles at what she probably thinks is an exaggeration."
        li "Surely you're not that bad? This vacation offers you the opportunity to get in plenty of practice. And you did remember to bring along a dictionary, did you not?"
        hi "I did, though I was kinda hoping that with you along, we weren't going to need it."

        show lilly basic_pout_cas
        with chchange

        "Lilly makes a face as if I just asked her to rejoin the student council."

        show lilly basic_displeased_cas
        with chchange

        li "I don't mind playing interpretor every now and then, but I won't be around you 24/7."
        li "Unless you intend to stay in your bedroom all the time while I'm away, which would be a terrible waste of opportunity, you'll have to interact with others without any help on my part at some point or another."

        show hanako basic_normal_cas
        with chchange

        hi "Huh? What do you mean away?"
        li "Initially, my parents were reluctant to pay for two additional tickets. They changed their minds when Akira pointed out that while I was spending time with them, one accompanying friend would just spend a lot of time alone. Two accompanying friends could keep each other company."
        li "I did come here to spend time with my mother and father, so during those times I won't always be able to hang out with you."
        "I kind of understand where Lilly's coming from, though with Hanako and me, any kind of contact with the locals is still going to be tricky."
        "English is pretty much my worst subject at school and conversations with people here will probably be difficult and drawn-out if I have to do the talking."
        "Hanako's English, while definitely not on Lilly's level, is still a lot better than mine, but her lack of social skills and unease in dealing with people in general completely destroys the advantage she has over me in the language department."
        "All in all, neither of us is really fit to interact with people around here."
        "Still, seeing how unique an opportunity we were given by being able to come here, I don't think even Hanako wants us to spend the entire vacation holed up in our room."

        show hanako basic_worry_cas
        show lilly basic_smile_cas
        with chchange

        hi "So I suppose Hanako and I will have to find our way around. Any tips?"

        show lilly basic_smileclosed_cas
        with chchange

        li "Being a city with an extensive tourist industry, Inverness is easy to navigate by coach. There's a bus stop not too far from here."
        li "I'll ask Allison to stop by the tourist information center tomorrow and get you two bus cards, some pamphlets and maps with the major tourist attractions and bus routes."
        hi "That would probably be a good start."

        show lilly basic_smile_cas
        with chchange

        li "Are you two more interested in nature or culture? There's plenty of both in the area."
        hi "I think I'd personally like a bit of both. I think the smartest thing is for Hanako and me to do the scenic parts by ourselves and save the cultural spots for when you're coming along so you can translate whatever the tour guides or videos have to say and we don't miss anything."

        show hanako basic_smile_cas
        with chchange

        "I look at Hanako who briefly nods to indicate her agreement."

        show lilly basic_satisfied_cas
        with chchange

        li "That sounds like a very good idea, Hisao. I'm sure we'll still get plenty of those opportunities."

        show lilly basic_smileclosed_cas
        with chchange

        "At this moment, Allison returns with the first course of our meal. The soup tastes differently from the soup I usually buy from the cafeteria, but it's quite good nevertheless, and I think I can learn to really like this."
        "As we eat, I take some time to look the girls over."
        # TODO Is this dialogue necessary?
        "Lilly's wearing a blouse with fairly short sleeves, sandals and a skirt that reaches down to her knees - a lot less conservative than her usual skirts which reach all the way to her ankles."
        "Hanako's pants aren't unlike the ones she usually wears, but her blouse is slightly lighter. It still has long sleeves to cover the burns on her right arm, but the area around her neckline shows slightly more skin than her usual outfit does."

        show lilly basic_smile_cas
        with chchange

        li "Do the two of you have any plans for the evening?"
        hi "I promised Hanako a tour of the mansion. She was really eager to see the rest of it for herself."

        show lilly basic_oops_cas
        with chchange

        li "Did you really sleep for nearly 24 hours, Hanako?"

        show hanako emb_downsmile_cas
        with chchange

        ha "N-Not really. I woke up at ten o' clock. I just… wanted to finish m-my book first before going out."

        show lilly basic_weaksmile_cas
        with chchange

        "Wow, that means she's been just lying next to me for nearly 7 hours. No wonder she was so eager for me to get up. I don't exactly buy the excuse about the book, and from the looks of it, Lilly doesn't either."
        "It's a whole lot more likely that Hanako decided not to leave our room because she was afraid of awkward encounters with the housekeeping staff or worse—with Lilly's parents. She probably dreaded the idea of making a poor first impression on them."

        show lilly basic_cheerful_cas
        with chchange

        li "I'll be honored to give you a small tour, Hanako. Any other plans?"

        show hanako emb_smile_cas
        with chchange

        ha "No. Perhaps take a walk along the l-lake. We could see it from our bedroom window."

        show lilly basic_smileclosed_cas
        with chchange

        li "Akira and I often went there during our last stay here. She often challenged me to swimming matches, though I'm afraid it was never much of a competition. It's not truly a lake by the way, despite Akira calling it that."
        li "Moray Firth is more like a cove or a bay. It's connected to the North Sea, though since it's almost completely surrounded by land, you don't experience a lot of waves."
        hi "I didn't even bring my swimming trunks, so I hope she's not thinking of challenging any of us."

        show lilly basic_reminisce_cas
        with chchange

        li "I don't think she will. She has her job here now, so after tonight we probably won't be interacting with her a lot, unfortunately."
        hi "Tonight?"

        show lilly basic_smileclosed_cas
        with chchange

        li "She called me about an hour ago and invited me to come and visit one of the local pubs with her tonight. I was hoping you'd be willing to come along as well."
        hi "She's not wasting time in making good on her promise to take you out for a drink once you got here."
        li "It appears so. We'll be heading to downtown Inverness and see if we can find a place we like. I think she'd enjoy your company as well."
        hi "That jazz club outing last time was pretty fun."
        "I look in Hanako's direction to see what she thinks."
        "She seems to take a moment to consider the offer and then softly nods to indicate her agreement."

        show hanako basic_bashful_cas
        with chchange

        ha "O-Okay then."

        show lilly basic_planned_cas
        with chchange

        li "Wonderful. Let's make tonight the first of many memorable experiences."

        stop music fadeout 2.0

        scene black
        with Dissolve(2.0)

        if _in_replay:
            return

    return